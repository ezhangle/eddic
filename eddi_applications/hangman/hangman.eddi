const int SIZE = 20;
const int LENGTH = 14;
str word = "asymptotically";

void main(){
    char guessed[SIZE];
    int state = 0;
    int count = -1;

    println("Try to guess the word");
    print_word(guessed);

    while(state == 0){
        print("Guess a char: ");
        guessed[++count] = get_char();
        println();

        print_word(guessed);

        if(complete(guessed)){
            println("Congratulations, you guessed it !");
            state = 1;
        } else if(count == 19){
            println("Too many guesses. Game over !");
            state = 2;
        }
    }
}

char get_char(){
    char c = read_char();

    while(c == (char) 10){
        c = read_char();
    }

    return c;
}

bool complete(char[] guessed){
    for(int i = 0; i < LENGTH; ++i){
        if(!contains(guessed, word[i])){
            return false;
        }
    }

    return true;
}

bool contains(char[] guessed, char c){
    for(int i = 0; i < SIZE; ++i){
        if(guessed[i] == c){
            return true;
        }
    }

    return false;
}

void print_word(char[] guessed){
    for(int i = 0; i < LENGTH; ++i){
        if(contains(guessed, word[i])){
            print(word[i]);
        } else {
            print("_");
        }

        print(" ");
    }

    println();
}
